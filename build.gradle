import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'groovy'
}

repositories {
    mavenCentral()
}

def slf4jVersion = '2.0.16'
def djlVersion = '0.31.1'
def nd4jVersion= '1.0.0-M2.1'

dependencies {
    implementation 'org.apache.groovy:groovy:5.0.0-alpha-12'
    implementation 'info.debatty:java-string-similarity:2.0.0'
    implementation 'org.apache.commons:commons-text:1.13.0'
    implementation 'commons-codec:commons-codec:1.18.0'
    implementation 'com.diogonunes:JColor:5.5.1'
    implementation 'org.openrefine:main:3.9-beta1'

    implementation "ai.djl:api:$djlVersion"
    implementation "ai.djl.pytorch:pytorch-engine:$djlVersion"
    implementation "ai.djl.huggingface:tokenizers:$djlVersion"
    implementation "ai.djl:model-zoo:$djlVersion"
    implementation "org.slf4j:slf4j-simple:$slf4jVersion"
    runtimeOnly "ai.djl.tensorflow:tensorflow-engine:$djlVersion"
    runtimeOnly "ai.djl.tensorflow:tensorflow-model-zoo:$djlVersion"
    //runtimeOnly "ai.djl.tensorflow:tensorflow-native-auto:2.10.1"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        runtimeOnly "ai.djl.tensorflow:tensorflow-native-cpu:2.16.1:win-x86_64"
        runtimeOnly 'org.bytedeco:javacpp:1.5.11:windows-x86_64'
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        runtimeOnly "ai.djl.tensorflow:tensorflow-native-cpu:2.16.1:osx-aarch64"
        runtimeOnly 'org.bytedeco:javacpp:1.5.11:ios-arm64'
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        runtimeOnly "ai.djl.tensorflow:tensorflow-native-cpu:2.16.1:linux-x86_64"
        runtimeOnly 'org.bytedeco:javacpp:1.5.11:linux-x86_64'
    }
//    runtimeOnly "org.slf4j:slf4j-jdk14:$slf4jVersion"
    runtimeOnly("org.nd4j:nd4j-native-platform:$nd4jVersion") {
        exclude group: 'org.bytedeco', module: 'javacpp-presets'
        exclude group: 'org.bytedeco', module: 'javacpp'
        exclude group: 'org.bytedeco', module: 'javacpp-platform'
    }
    implementation("org.deeplearning4j:deeplearning4j-core:$nd4jVersion") {
        exclude group: 'org.bytedeco', module: 'javacpp-presets'
        exclude group: 'org.bytedeco', module: 'javacpp'
        exclude group: 'org.bytedeco', module: 'javacpp-platform'
        exclude group: 'com.twelvemonkeys.imageio'
        exclude group: 'com.github.jai-imageio'
        exclude group: 'org.datavec', module: 'datavec-data-image'
        exclude group: 'com.github.oshi'
        exclude group: 'org.deeplearning4j', module: 'deeplearning4j-ui-components'
        exclude group: 'org.deeplearning4j', module: 'deeplearning4j-modelimport'
    }
    implementation("org.deeplearning4j:deeplearning4j-nlp:$nd4jVersion") {
        exclude group: 'org.bytedeco', module: 'javacpp-presets'
        exclude group: 'org.bytedeco', module: 'javacpp'
        exclude group: 'org.bytedeco', module: 'javacpp-platform'
        exclude group: 'com.twelvemonkeys.imageio'
    }
    implementation('org.bytedeco:javacpp:1.5.11') {
        transitive = false
    }
}

sourceSets.main.allSource.files.each { file ->
    def name = "${file.name - '.groovy'}"
    tasks.register("run$name", JavaExec) {
        dependsOn compileGroovy
        group = 'Application'
        description = "Run $file.name as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
}
